(* terminals *)

COUNTERCHANGED = #"\bcounterchanged\b"
FIMBRIATED
    = #"\bfimbriated\b"
    | #"\bfimbriation\b"

A = #"\ba\b"
  | #"\ban\b"
<AND> = #"\band\b"
    | #"&"
    | #","
IN = #"\bin\b"
OF = #"\bof\b"
THE = #"\bthe\b"
LIKE = #"\blike\b"
WITH = #"\bwith\b"
CHARGED = #"\bcharged\b"
TRACTS = #"\btracts\b"
X = #"x"
TIMES = #"\btimes\b"
TINCTURE = #"\btincture\b"
FIELD = #"\bfield\b"
SAME = #"\bsame\b"
SEMY
    = #"\bsemy\b"
    | #"\bsemé(?=([ ,&]|$))"
    | #"\bsemée\b"

DOUBLY
    = #"\bdoubly\b"
    | #"\bdouble\b"
    | #"\btwo\b"
    | #"\b2\b"
BETWEEN = #"\bbetween\b"
COTTISED
    = #"\bcottised\b"
    | #"\bcotticed\b"
    | #"\bcottises\b"
    | #"\bcottices\b"
    | #"\bcottising\b"
    | #"\bcotticing\b"
    | #"\bbendorsed\b"
    | #"\bbendorsing\b"

NUMBER = #"[1-9][0-9]*"
DIGIT-WORD
    = #"\bone\b"
    | #"\btwo\b"
    | #"\bthree\b"
    | #"\bfour\b"
    | #"\bfive\b"
    | #"\bsix\b"
    | #"\bseven\b"
    | #"\beight\b"
    | #"\bnine\b"
MULTI-WORD
    = #"\bdouble\b"
    | #"\btriple\b"
TEEN-WORD
    = #"\bten\b"
    | #"\beleven\b"
    | #"\btwelve\b"
    | #"\bthirteen\b"
    | #"\bfourteen\b"
    | #"\bfifteen\b"
    | #"\bsixteen\b"
    | #"\bseventeen\b"
    | #"\beighteen\b"
    | #"\bnineteen\b"
MULTIPLE-OF-TEN-WORD
    = #"\btwenty\b"
    | #"\bthirty\b"
    | #"\bfourty\b"
    | #"\bfifty\b"
    | #"\bsixty\b"
    | #"\bseventy\b"
    | #"\beighty\b"
    | #"\bninety\b"

ORDINAL-NUMBER
    = #"\b1st\b"
    | #"\b2nd\b"
    | #"\b3rd\b"
    | #"\b4th\b"
    | #"\b5th\b"
    | #"\b6th\b"
    | #"\b7th\b"
    | #"\b8th\b"
    | #"\b9th\b"
    | #"\b10th\b"
    | #"\b11th\b"
    | #"\b12th\b"
    | #"\b13th\b"
    | #"\b14th\b"
    | #"\b15th\b"
    | #"\b16th\b"
    | #"\b17th\b"
    | #"\b18th\b"
    | #"\b19th\b"
    | #"\b20th\b"
ORDINAL-NUMBER-DOT
    = #"\b1[.](?=([ ,&]|$))"
    | #"\b2[.](?=([ ,&]|$))"
    | #"\b3[.](?=([ ,&]|$))"
    | #"\b4[.](?=([ ,&]|$))"
    | #"\b5[.](?=([ ,&]|$))"
    | #"\b6[.](?=([ ,&]|$))"
    | #"\b7[.](?=([ ,&]|$))"
    | #"\b8[.](?=([ ,&]|$))"
    | #"\b9[.](?=([ ,&]|$))"
    | #"\b10[.](?=([ ,&]|$))"
    | #"\b11[.](?=([ ,&]|$))"
    | #"\b12[.](?=([ ,&]|$))"
    | #"\b13[.](?=([ ,&]|$))"
    | #"\b14[.](?=([ ,&]|$))"
    | #"\b15[.](?=([ ,&]|$))"
    | #"\b16[.](?=([ ,&]|$))"
    | #"\b17[.](?=([ ,&]|$))"
    | #"\b18[.](?=([ ,&]|$))"
    | #"\b19[.](?=([ ,&]|$))"
    | #"\b20[.](?=([ ,&]|$))"
ORDINAL-ROMAN-DOT
    = #"\bi[.](?=([ ,&]|$))"
    | #"\bii[.](?=([ ,&]|$))"
    | #"\biii[.](?=([ ,&]|$))"
    | #"\biv[.](?=([ ,&]|$))"
    | #"\biiii[.](?=([ ,&]|$))"
    | #"\bv[.](?=([ ,&]|$))"
    | #"\bvi[.](?=([ ,&]|$))"
    | #"\bvii[.](?=([ ,&]|$))"
    | #"\bviii[.](?=([ ,&]|$))"
    | #"\bix[.](?=([ ,&]|$))"
    | #"\bx[.](?=([ ,&]|$))"
    | #"\bxi[.](?=([ ,&]|$))"
    | #"\bxii[.](?=([ ,&]|$))"
    | #"\bxiii[.](?=([ ,&]|$))"
    | #"\bxiv[.](?=([ ,&]|$))"
    | #"\bxv[.](?=([ ,&]|$))"
    | #"\bxvi[.](?=([ ,&]|$))"
    | #"\bxvii[.](?=([ ,&]|$))"
    | #"\bxviii[.](?=([ ,&]|$))"
    | #"\bxix[.](?=([ ,&]|$))"
    | #"\bxx[.](?=([ ,&]|$))"
ORDINAL-WORD
    = #"\bfirst\b"
    | #"\bsecond\b"
    | #"\bthird\b"
    | #"\bfourth\b"
    | #"\bfifth\b"
    | #"\bsixth\b"
    | #"\bseventh\b"
    | #"\beighth\b"
    | #"\bninth\b"
    | #"\btenth\b"
    | #"\beleventh\b"
    | #"\btwelveth\b"
    | #"\bthirteenth\b"
    | #"\bfourteenth\b"
    | #"\bfifteenth\b"
    | #"\bsixteenth\b"
    | #"\bseventeenth\b"
    | #"\beighteenth\b"
    | #"\bnineteenth\b"
    | #"\btwentieth\b"

OR = #"\bor\b"
   | #"\bd'or\b"
ARGENT
    = #"\bargent\b"
    | #"\bd'argent\b"
SABLE = #"\bsable\b"
AZURE
    = #"\bazure\b"
    | #"\bd'azure\b"
GULES = #"\bgules\b"
PURPURE = #"\bpurpure\b"
VERT = #"\bvert\b"
TENNE
    = #"\btenne\b"
    | #"\btenné(?=([ ,&]|$))"
ORANGE
    = #"\borange\b"
    | #"\bd'orange\b"
SANGUINE = #"\bsanguine\b"
MURREY = #"\bmurrey\b"
BLEU-CELESTE
    = #"\bbleu-+celeste\b"
    | #"\bbleu +celeste\b"
ROSE = #"\brose\b"
COPPER = #"\bcopper\b"
PROPER = #"\bproper\b"
WHITE = #"\bwhite\b"
BRUNATRE
    = #"\bbrunatre\b"
    | #"\bbrunâtre\b"
BUFF = #"\bbuff\b"

ERMINE = #"\bermine\b"
ERMINES = #"\bermines\b"
ERMINOIS = #"\berminois\b"
PEAN = #"\bpean\b"

BARRY
    = #"\bbarry\b"
    | #"\bbarruly\b"
    | #"\burely\b"
BENDY = #"\bbendy\b"
BENDY-SINISTER
    = #"\bbendy +sinister\b"
    | #"\bbendy\b" #"\bsinister\b"
CHEQUY = #"\bchequy\b"
CHEVRONNY = #"\bchevronny\b"
FRETTY = #"\bfretty\b"
GYRONNY = #"\bg[yi]ronny\b"
LOZENGY = #"\blozengy\b"
MASONY
    = #"\bmasony\b"
    | #"\bmasoned\b"
PALY = #"\bpaly\b"
PAPELLONY = #"\bpapellony\b"
PER-BEND
    = #"\bper +bend\b"
    | #"\bparty +per +bend\b"
PER-BEND-SINISTER
    = #"\bper +bend +sinister\b"
    | #"\bper +bend\b" #"\bsinister\b"
    | #"\bparty +per +bend +sinister\b"
    | #"\bparty +per +bend\b" #"\bsinister\b"
PER-CHEVRON
    = #"\bper +chevron\b"
    | #"\bparty +per +chevron\b"
PER-FESS
    = #"\bper +fess\b"
    | #"\bparty +per +fess\b"
PER-PALE
    = #"\bper +pale\b"
    | #"\bparty +per +pale\b"
PER-PILE
    = #"\bper +pile\b"
    | #"\bparty +per +pile\b"
PER-SALTIRE
    = #"\bper +saltire\b"
    | #"\bparty +per +saltire\b"
POTENTY = #"\bpotenty\b"
QUARTERED
    = #"\bquartered\b"
    | #"\bquarterly\b"
TIERCED-PER-FESS = #"\btierced +per +fess\b"
TIERCED-PER-PALE = #"\btierced +per +pale\b"
TIERCED-PER-PALL = #"\btierced +per +pall\b"
VAIRY = #"\bvairy\b"

PALE = #"\bpales?\b"
PALLET = #"\bpallets?\b"
FESS
    = #"\bfess(es?)?\b"
    | #"\bbars?\b"
BARRULET = #"\bbarrulets?\b"
CHIEF = #"\bchief\b"
BASE = #"\bbase\b"
BEND = #"\bbends?\b"
BENDLET = #"\bbendlets?\b"
BEND-SINISTER
    = #"\bbends? +sinister\b"
    | #"\bbends?\b" #"\bsinister\b"
BENDLET-SINISTER
    = #"\bbendlets? +sinister\b"
    | #"\bbendlets?\b" #"\bsinister\b"
CROSS = #"\bcross(es)?\b"
SALTIRE = #"\bsaltires?\b"
CHEVRON = #"\bchevrons?\b"
CHEVRONNEL = #"\bchevronnels?\b"
PALL = #"\bpalls?\b"
PILE = #"\bpiles?\b"
GORE = #"\bgores?\b"
QUARTER = #"\bquarters?\b"
POINT
    = #"\bpoints?\b"
LABEL-POINT = #"\bpoints?\b"
STAR-POINT = #"\bpoints?\b"
BORDURE = #"\bbordures?\b"
ORLE
    = #"\borles?\b"
    | #"\btressures?\b"
LABEL = #"\blabels?\b"

STRAIGHT = #"\bstraight\b"
INVECTED = #"\binvected\b"
ENGRAILED = #"\bengrailed\b"
EMBATTLED = #"\bembattled\b"
EMBATTLED-GRADY
    = #"\bembattled grady\b"
    | #"\bembattled-+grady\b"
    | #"\bembattled\b" #"\bgrady\b"
EMBATTLED-IN-CROSSES
    = #"\bembattled +in +crosses\b"
    | #"\bembattled-+in-+crosses\b"
    | #"\bembattled\b" #"\bin crosses\b"
POTENTY = #"\bpotenty\b"
INDENTED = #"\bindented\b"
DANCETTY = #"\bdancetty\b"
WAVY
    = #"\bwavy\b"
    | #"\bundy\b"
NEBULY = #"\bnebuly\b"
DOVETAILED
    = #"\bdovetailed\b"
    | #"\bdove +tailed\b"
    | #"\bdove-+tailed\b"
RAGULY = #"\braguly\b"
THORNY = #"\bthorny\b"
URDY = #"\burdy\b"
FIR-TREE-TOPPED
    = #"\bfir +tree +topped\b"
    | #"\bfir-+tree-+topped\b"
FIR-TWIGGED
    = #"\bfir +twigged\b"
    | #"\bfir-+twigged\b"
WOLF-TOOTHED
    = #"\bwolf +toothed\b"
    | #"\bwolf-+toothed\b"
RAYONNY = #"\brayonny\b"
ANGLED = #"\bangled\b"
BEVILLED = #"\bbevilled\b"
ENARCHED = #"\benarched\b"

HUMETTY
    = #"\bhumetty\b"
    | #"\bcouped\b"
TRUNCATED
    = #"\btruncated\b"
VOIDED = #"\bvoided\b"
ENHANCED = #"\benhanced\b"
DEHANCED
    = #"\bdehanced\b"
    | #"\bdebased\b"
    | #"\babased\b"
    | #"\babaisse\b"
REVERSED
    = #"\breversed\b"
    | #"\binverted\b"
THROUGHOUT = #"\bthroughout\b"

DEXTER = #"\bdexter\b"
SINISTER = #"\bsinister\b"
CHIEF = #"\bchief\b"
BASE = #"\bbase\b"
DEXTER-CHIEF
    = #"\bdexter[ -]chief\b"
    | #"\bdexter" #"chief\b"
CHIEF-DEXTER
    = #"\bchief[ -]dexter\b"
    | #"\bchief" #"dexter\b"
SINISTER-CHIEF
    = #"\bsinister[ -]chief\b"
    | #"\bsinister" #"chief\b"
CHIEF-SINISTER
    = #"\bchief[ -]sinister\b"
    | #"\bchief" #"sinister\b"
DEXTER-BASE
    = #"\bdexter[ -]base\b"
    | #"\bdexter" #"base\b"
BASE-DEXTER
    = #"\bbase[ -]dexter\b"
    | #"\bbase" #"dexter\b"
SINISTER-BASE
    = #"\bsinister[ -]base\b"
    | #"\bsinister" #"base\b"
BASE-SINISTER
    = #"\bbase[ -]sinister\b"
    | #"\bbase" #"sinister\b"

CADENT = #"\bcadent\b"
COUCHANT = #"\bcouchant\b"
COURANT = #"\bcourant\b"
DORMANT = #"\bdormant\b"
PASCUANT = #"\bpascuant\b"
PASSANT = #"\bpassant\b"
RAMPANT = #"\brampant\b"
SALIENT = #"\bsalient\b"
SEGREANT = #"\bsegreant\b"
SEJANT = #"\bsejant\b"
SEJANT-ERECT
    = #"\bsejant[ -]erect\b"
    | #"\bsejant\b" "\berect\b"
STATANT = #"\bstatant\b"
ADDORSED = #"\baddorsed\b"
COMBATANT = #"\bcombatant\b"
RESPECTANT = #"\brespectant\b"
CLOSE = #"\bclose\b"
DISPLAYED = #"\bdisplayed\b"
IN-HER-PIETY
    = #"\bin her piety\b"
    | #"\bin-her-piety\b"
ISSUANT = #"\bissuant\b"
OVERT = #"\bovert\b"
RECURSANT = #"\brecursant\b"
RISING = #"\brising\b"
VIGILANT = #"\bvigilant\b"
VOLANT = #"\bvolant\b"
VULNING = #"\bvulning\b"
HAURIANT = #"\bhauriant\b"
NAIANT = #"\bnaiant\b"
URINANT = #"\burinant\b"
GLISSANT = #"\bglissant\b"
NOWED = #"\bnowed\b"

TO-DEXTER = #"\bto[ -]dexter\b"
TO-SINISTER = #"\bto[ -]sinister\b"
AFFRONTE = #"\baffronte\b"
EN-ARRIERE = #"\ben[ -]arriere\b"
GUARDANT = #"\bguardant\b"
IN-TRIAN-ASPECT
    = #"\bin trian aspect\b"
    | #"\bin-trian-aspect\b"
REGUARDANT = #"\breguardant\b"
SALIENT = #"\bsalient\b"

ARMED = #"\barmed\b"
ATTIRED = #"\battired\b"
BEAKED = #"\bbeaked\b"
COMBED = #"\bcombed\b"
EYED = #"\beyed\b"
EYED-PEACOCK
    = #"\beyed +peacock\b"
    | #"\beyed-peacock\b"
    | #"\bpeacock +eyed\b"
    | #"\bpeacock-eyed\b"
FINNED = #"\bfinned\b"
INCENSED = #"\bincensed\b"
LANGUED = #"\blangued\b"
LEGGED = #"\blegged\b"
MANED = #"\bmaned\b"
ORBED = #"\borbed\b"
TAILED = #"\btailed\b"
TOOTHED = #"\btoothed\b"
UNGULED = #"\bunguled\b"
WATTLED = #"\bwattled\b"
WINGED = #"\bwinged\b"
BARBED = #"\bbarbed\b"
BLADED = #"\bbladed\b"
FRUCTED = #"\bfructed\b"
LEAVED = #"\bleaved\b"
SEEDED = #"\bseeded\b"
SLIPPED = #"\bslipped\b"
BARRED = #"\bbarred\b"
TRIMMED = #"\btrimmed\b"
CAPARISONED = #"\bcaparisoned\b"
COLLARED = #"\bcollared\b"
ERASED = #"\berased\b"
GARNISHED = #"\bgarnished\b"
HANDLED = #"\bhandled\b"
HILTED = #"\bhilted\b"
ILLUMINATED = #"\billuminated\b"
HOODED = #"\bhooded\b"
MASONED = #"\bmasoned\b"
POMMELED = #"\bpommeled\b"
STRINGED = #"\bstringed\b"

MIRRORED = #"\bmirrored\b"
ERASED = #"\berased\b"

ROUNDEL = #"\broundels?\b"
ANNULET = #"\bannulets?\b"
BILLET = #"\bbillets?\b"
ESCUTCHEON
    = #"\binescutcheons?\b"
    | #"\bescutcheons?\b"
LOZENGE = #"\blozenges?\b"
FUSIL = #"\bfusils?\b"
MASCLE = #"\bmascles?\b"
RUSTRE = #"\brustres?\b"
STAR
    = #"\bstars?\b"
    | #"\bmullets?\b"
CRESCENT = #"\bcrescents?\b"

PALEWISE = #"\bpale[ -]?wise\b"
FESSWISE = #"\bfess[ -]?wise\b"
BENDWISE = #"\bbend[ -]?wise\b"
BENDWISE-SINISTER
    = #"\bbend[ -]?wise +sinister\b"
    | #"\bbend[ -]?wise\b" "\bsinister\b"
CHEVRONWISE = #"\bchevron[ -]?wise\b"
IN-ANNULLO
    = #"\bin +annullo\b"
IN-ORLE = #"\bin +orle\b"
ARRANGED
    = #"\barranged\b"
    | #"\barranged +as\b"

(* rules *)

blazon
    = root-field

charged-with
    = CHARGED? WITH

root-field
    = root-variation <charged-with> component (AND* components)?
    | root-variation
    | root-variation (AND* components)?
    | <"("> root-field <")">

field
    = variation <charged-with> component (AND* components)?
    | variation
    | variation (AND* components)?
    | <"("> field <")">

root-variation
    = root-plain
    | partition

variation
    = plain
    | partition

root-plain
    = tincture

plain
    = tincture
    | COUNTERCHANGED

of-the
    = OF? THE
    | IN? THE
    | LIKE? THE

tincture
    = OR
    | ARGENT
    | SABLE
    | AZURE
    | GULES
    | PURPURE
    | VERT
    | TENNE
    | ORANGE
    | SANGUINE
    | MURREY
    | BLEU-CELESTE
    | ROSE
    | COPPER
    | PROPER
    | WHITE
    | BRUNATRE
    | BUFF
    | ERMINE
    | ERMINES
    | ERMINOIS
    | PEAN
    | <of-the> ordinal <TINCTURE>?
    | <of-the> FIELD
    | <of-the> SAME

(* used for suggestion labeling *)
partition-type
    = BARRY
    | BENDY
    | BENDY-SINISTER
    | CHEQUY
    | CHEVRONNY
    | FRETTY
    | GYRONNY
    | LOZENGY
    | MASONY
    | PALY
    | PAPELLONY
    | PER-BEND
    | PER-BEND-SINISTER
    | PER-CHEVRON
    | PER-FESS
    | PER-PALE
    | PER-PILE
    | PER-SALTIRE
    | POTENTY
    | QUARTERED
    | TIERCED-PER-FESS
    | TIERCED-PER-PALE
    | TIERCED-PER-PALL
    | VAIRY

(* used for suggestion labeling *)
layout-words
    = OF
    | X
    | TIMES
    | TRACTS

horizontal-layout
    = amount

vertical-layout
    = vertical-layout-implicit
    | vertical-layout-explicit

vertical-layout-implicit
    = amount

vertical-layout-explicit
    = amount TRACTS

layout-times
    = X
    | TIMES
    | AND

layout
    = horizontal-layout
    | vertical-layout-explicit
    | horizontal-layout layout-times? vertical-layout
    | vertical-layout-explicit layout-times? horizontal-layout

field-location
    = DEXTER
    | SINISTER
    | CHIEF
    | BASE
    | FESS
    | DEXTER-CHIEF
    | CHIEF-DEXTER
    | SINISTER-CHIEF
    | CHIEF-SINISTER
    | DEXTER-BASE
    | BASE-DEXTER
    | SINISTER-BASE
    | BASE-SINISTER

field-reference
    = ordinal-including-dot
    | (<IN> | <WITH>)? field-location

partition-field
    = (field-reference (AND* field-reference)*)? root-field

partition-field-plain
    = (field-reference (AND* field-reference)*)? root-plain

partition
    = PER-PALE line? partition-field AND+ partition-field
    | (PER-FESS
       | PER-BEND
       | PER-BEND-SINISTER) (enhanced-dehanced* line?
                       | line? enhanced-dehanced*) partition-field AND+ partition-field
    | PER-CHEVRON (enhanced-dehanced-reversed* line?
                       | line? enhanced-dehanced-reversed*) partition-field AND+ partition-field
    | PER-PILE (REVERSED? (line (AND+ line)?)?
                | (line (AND+ line)?)? REVERSED?) partition-field AND+ partition-field (AND+ partition-field)?
    | TIERCED-PER-PALE (line (AND+ line)?)? partition-field AND+ partition-field (AND+ partition-field)?
    | TIERCED-PER-FESS (enhanced-dehanced* (line (AND+ line)?)?
                        | (line (AND+ line)?)? enhanced-dehanced*) partition-field AND+ partition-field (AND+ partition-field)?
    | TIERCED-PER-PALL (enhanced-dehanced-reversed* (line (AND+ line)? (AND+ line)?)?
                        | (line (AND+ line)? (AND+ line)?)? enhanced-dehanced-reversed*) partition-field AND+ partition-field (AND+ partition-field)?
    | (PER-SALTIRE
       | QUARTERED) (enhanced-dehanced* line?
                     | line? enhanced-dehanced*) partition-field AND+ partition-field (AND+ partition-field)? (AND+ partition-field)?
    | GYRONNY (enhanced-dehanced* line?
               | line? enhanced-dehanced*) partition-field AND+ partition-field (AND+ partition-field)? (AND+ partition-field)? (AND+ partition-field)? (AND+ partition-field)? (AND+ partition-field)? (AND+ partition-field)?
    | QUARTERED enhanced-dehanced* partition-field AND+ partition-field (AND+ partition-field)*
    | (CHEQUY
       | LOZENGY
       | VAIRY
       | POTENTY
       | PAPELLONY
       | MASONY
       | FRETTY) (OF? layout)? partition-field-plain AND+ partition-field-plain
    | PALY (OF? horizontal-layout)? partition-field AND+ partition-field
    | (BARRY
       | BENDY
       | BENDY-SINISTER
       | CHEVRONNY) (OF? vertical-layout)? partition-field AND+ partition-field

components
    = component (AND+ component)*

component
    = ordinary-group
    | charge-group
    | semy

number-word
    = DIGIT-WORD
    | <A>? MULTI-WORD
    | TEEN-WORD
    | MULTIPLE-OF-TEN-WORD
    | MULTIPLE-OF-TEN-WORD #"-?" DIGIT-WORD

ordinal
    = ORDINAL-NUMBER
    | ORDINAL-WORD

ordinal-including-dot
    = ORDINAL-NUMBER
    | ORDINAL-WORD
    | ORDINAL-NUMBER-DOT
    | ORDINAL-ROMAN-DOT

amount
    = A
    | NUMBER
    | number-word

<between-or-with>
    = BETWEEN
    | WITH
cottise
    = (line (AND+ line)?)? field
cottising
    = between-or-with? COTTISED WITH? cottise
    | between-or-with? COTTISED WITH? cottise AND+ cottise
    | between-or-with? DOUBLY COTTISED WITH? cottise
    | between-or-with? DOUBLY COTTISED WITH? cottise AND+ cottise

(* used for suggestion labeling *)
cottising-word
    = COTTISED
    | DOUBLY
    | BETWEEN
    | WITH

ordinary-group
    = amount? ordinary

(* used for suggestion labeling *)
ordinary-type
    = BARRULET
    | BASE
    | BEND
    | BENDLET
    | BEND-SINISTER
    | BENDLET-SINISTER
    | BORDURE
    | CHEVRON
    | CHEVRONNEL
    | CHIEF
    | CROSS
    | FESS
    | GORE
    | LABEL
    | ORLE
    | PALE
    | PALL
    | PILE
    | POINT
    | QUARTER
    | SALTIRE

(* used for suggestion labeling *)
ordinary-option
    = humetty-voided
    | enhanced-dehanced
    | reversed-throughout

<humetty-voided>
    = AND* HUMETTY
    | AND* VOIDED

<humetty-voided-truncated-dovetailed>
    = humetty-voided
    | AND* TRUNCATED
    | AND* DOVETAILED

<enhanced-dehanced>
    = AND* ENHANCED
    | AND* DEHANCED

<enhanced-dehanced-reversed>
    = enhanced-dehanced
    | AND* REVERSED

<reversed-throughout>
    = AND* REVERSED
    | AND* THROUGHOUT

<humetty-voided-enhanced-dehanced>
    = humetty-voided
    | enhanced-dehanced

<humetty-voided-enhanced-dehanced-reversed>
    = humetty-voided
    | enhanced-dehanced
    | AND* REVERSED

<humetty-voided-reversed-throughout>
    = humetty-voided
    | reversed-throughout

<dexter-sinister>
    = AND* DEXTER
    | AND* SINISTER

<humetty-voided-enhanced-dehanced-dexter-sinister>
    = humetty-voided
    | enhanced-dehanced
    | dexter-sinister

<of-or-with>
    = OF
    | WITH

label-points
    = (of-or-with? amount LABEL-POINT?)

ordinary
    = LABEL (humetty-voided-truncated-dovetailed* label-points?
             | label-points? humetty-voided-truncated-dovetailed*) fimbriation? field
    | BORDURE line? field
    | GORE dexter-sinister? line? field
    | (CHIEF
       | BASE) (humetty-voided-enhanced-dehanced* line?
                 | line? humetty-voided-enhanced-dehanced*) field cottising?
    | POINT (humetty-voided-enhanced-dehanced-dexter-sinister* line?
                 | line? humetty-voided-enhanced-dehanced-dexter-sinister*) field cottising?
    | ORLE (line (AND+ line)?)? field
    | (CROSS
       | SALTIRE) (humetty-voided-enhanced-dehanced* (line (AND+ line)?)?
                   | (line (AND+ line)?)? humetty-voided-enhanced-dehanced*) field cottising?
    | QUARTER (humetty-voided-enhanced-dehanced-dexter-sinister* (line (AND+ line)?)?
                   | (line (AND+ line)?)? humetty-voided-enhanced-dehanced-dexter-sinister*) field cottising?
    | PILE (humetty-voided-reversed-throughout* (line (AND+ line)?)?
            | (line (AND+ line)?)? humetty-voided-reversed-throughout*) field cottising?
    | (PALE
       | PALLET) (humetty-voided* (line (AND+ line)?)?
                  | (line (AND+ line)?)? humetty-voided*) field (cottising (AND? cottising)?)?
    | (FESS
       | BARRULET
       | BEND
       | BENDLET
       | BEND-SINISTER
       | BENDLET-SINISTER) (humetty-voided-enhanced-dehanced* (line (AND+ line)?)?
                            | (line (AND+ line)?)? humetty-voided-enhanced-dehanced*) field (cottising (AND? cottising)?)?
    | (PALL
       | CHEVRON
       | CHEVRONNEL) (humetty-voided-enhanced-dehanced-reversed* (line (AND+ line (AND+ line)?)?)?
                      | (line (AND+ line (AND+ line)?)?)? humetty-voided-enhanced-dehanced-reversed*) field (cottising (AND? cottising (AND? cottising)?)?)?

line
    = line-type fimbriation?
    | fimbriation

line-type
    = STRAIGHT
    | INVECTED
    | ENGRAILED
    | EMBATTLED
    | EMBATTLED-GRADY
    | EMBATTLED-IN-CROSSES
    | POTENTY
    | INDENTED
    | DANCETTY
    | WAVY
    | NEBULY
    | DOVETAILED
    | RAGULY
    | THORNY
    | URDY
    | FIR-TREE-TOPPED
    | FIR-TWIGGED
    | WOLF-TOOTHED
    | RAYONNY
    | ANGLED
    | BEVILLED
    | ENARCHED

fimbriation
    = FIMBRIATED of-or-with? tincture
    | FIMBRIATED of-or-with? tincture AND? tincture

charge-group
    = amount? charge charge-arrangement? field tincture-modifier*
    | amount? charge charge-arrangement? tincture-modifier* field
    | amount? charge field tincture-modifier* charge-arrangement?
    | amount? charge field charge-arrangement? tincture-modifier*
    | amount? charge tincture-modifier* field charge-arrangement?
    | amount? charge tincture-modifier* charge-arrangement? field

semy
    = <SEMY> <of-or-with>? layout? charge field tincture-modifier*
    | <SEMY> <of-or-with>? layout? charge tincture-modifier* field

(* used for suggestion labeling *)
charge-standard-type
    = ROUNDEL
    | ANNULET
    | BILLET
    | ESCUTCHEON
    | LOZENGE
    | FUSIL
    | MASCLE
    | RUSTRE
    | STAR
    | CRESCENT

charge
    = charge-standard fimbriation?
    | charge-other fimbriation?

charge-arrangement
    = PALEWISE
    | FESSWISE
    | BENDWISE
    | BENDWISE-SINISTER
    | CHEVRONWISE
    | IN-ANNULLO
    | IN-ORLE
    | charge-grid

charge-grid
    = ARRANGED? amount (AND? amount)*

(* used for suggestion labeling *)
charge-option
    = mirrored-reversed

<mirrored-reversed>
    = AND* MIRRORED
    | AND* REVERSED

star-points
    = (of-or-with? amount STAR-POINT?)

(* used for bad charge type only *)
charge-standard-type
    = ROUNDEL
    | ANNULET
    | BILLET
    | ESCUTCHEON
    | LOZENGE
    | FUSIL
    | MASCLE
    | RUSTRE
    | STAR
    | CRESCENT

charge-standard
    = ROUNDEL
    | ANNULET
    | BILLET
    | ESCUTCHEON mirrored-reversed*
    | LOZENGE
    | FUSIL
    | MASCLE
    | RUSTRE
    | STAR (star-points? REVERSED?
            | REVERSED? star-points?)
    | CRESCENT REVERSED?

(* need one of them to have a valid charge-other-type rule,
   it will be overwritten by the placeholder, if it contains lion as well *)
custom-charge-type-lion
    = #'\blion\b'
    | #'\blions\b'

charge-other-type
    = custom-charge-type-lion
    {% charge-types %}

charge-other
    = charge-other-type AND* attitude? AND* facing? mirrored-reversed*

attitude
    = CADENT
    | COUCHANT
    | COURANT
    | DORMANT
    | PASCUANT
    | PASSANT
    | RAMPANT
    | SALIENT
    | SEGREANT
    | SEJANT
    | SEJANT-ERECT
    | STATANT
    | ADDORSED
    | COMBATANT
    | RESPECTANT
    | CLOSE
    | DISPLAYED
    | IN-HER-PIETY
    | ISSUANT
    | OVERT
    | RECURSANT
    | RISING
    | VIGILANT
    | VOLANT
    | VULNING
    | HAURIANT
    | NAIANT
    | URINANT
    | GLISSANT
    | NOWED

facing
    = TO-DEXTER
    | TO-SINISTER
    | AFFRONTE
    | EN-ARRIERE
    | GUARDANT
    | IN-TRIAN-ASPECT
    | REGUARDANT
    | SALIENT

tincture-modifier-type
    = ARMED
    | ATTIRED
    | BEAKED
    | COMBED
    | EYED
    | EYED-PEACOCK
    | FINNED
    | INCENSED
    | LANGUED
    | LEGGED
    | MANED
    | ORBED
    | TAILED
    | TOOTHED
    | UNGULED
    | WATTLED
    | WINGED
    | BARBED
    | BLADED
    | FRUCTED
    | LEAVED
    | SEEDED
    | SLIPPED
    | BARRED
    | TRIMMED
    | CAPARISONED
    | COLLARED
    | ERASED
    | GARNISHED
    | HANDLED
    | HILTED
    | ILLUMINATED
    | HOODED
    | MASONED
    | POMMELED
    | STRINGED

tincture-modifier
    = AND* tincture-modifier-type tincture

(* used to filter out badly named charges *)
bad-charge-type
    = ordinary-type
    | partition-type
    | line-type
    | charge-standard-type
    | amount
    | ordinal-including-dot
    | humetty-voided-truncated-dovetailed
    | enhanced-dehanced
    | reversed-throughout
    | mirrored-reversed
    | dexter-sinister
    | FIMBRIATED
    | tincture
    | charge-arrangement
    | attitude
    | facing
    | field-location
    | tincture-modifier-type
    | A
    | AND
    | IN
    | OF
    | THE
    | LIKE
    | WITH
    | CHARGED
    | TRACTS
    | X
    | TIMES
    | TINCTURE
    | FIELD
    | SAME
    | DOUBLY
    | BETWEEN
    | COTTISED
    | SEMY
