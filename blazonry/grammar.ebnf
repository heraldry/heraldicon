blazon
    = root-field

(* terminals *)

COUNTERCHANGED = #"\bcounterchanged\b"
FIMBRIATED
    = #"\bfimbriated\b"
    | #"\bfimbriation\b"

A = #"\ba\b"
  | #"\ban\b"
AND = #"\band\b"
    | #"&"
    | #","
OF = #"\bof\b"
WITH = #"\bwith\b"
CHARGED = #"\bcharged\b"
TRACTS = #"\btracts\b"
X = #"x"
TIMES = #"\btimes\b"

DOUBLY
    = #"\bdoubly\b"
    | #"\bdouble\b"
    | #"\btwo\b"
    | #"\b2\b"
BETWEEN = #"\bbetween\b"
COTTISED
    = #"\bcottised\b"
    | #"\bcotticed\b"
    | #"\bcottises\b"
    | #"\bcottices\b"
    | #"\bcottising\b"
    | #"\bcotticing\b"
    | #"\bendorsed\b"
    | #"\bendorsing\b"

NUMBER = #"[1-9][0-9]*"
DIGIT-WORD
    = #"\bone\b"
    | #"\btwo\b"
    | #"\bthree\b"
    | #"\bfour\b"
    | #"\bfive\b"
    | #"\bsix\b"
    | #"\bseven\b"
    | #"\beight\b"
    | #"\bnine\b"
MULTI-WORD
    = #"\bdouble\b"
    | #"\btriple\b"
TEEN-WORD
    = #"\bten\b"
    | #"\beleven\b"
    | #"\btwelve\b"
    | #"\bthirteen\b"
    | #"\bfourteen\b"
    | #"\bfifteen\b"
    | #"\bsixteen\b"
    | #"\bseventeen\b"
    | #"\beighteen\b"
    | #"\bnineteen\b"
MULTIPLE-OF-TEN-WORD
    = #"\btwenty\b"
    | #"\bthirty\b"
    | #"\bfourty\b"
    | #"\bfifty\b"
    | #"\bsixty\b"
    | #"\bseventy\b"
    | #"\beighty\b"
    | #"\bninety\b"

OR = #"\bor\b"
   | #"\bd'or\b"
ARGENT
    = #"\bargent\b"
    | #"\bd'argent\b"
SABLE = #"\bsable\b"
AZURE
    = #"\bazure\b"
    | #"\bd'azure\b"
GULES = #"\bgules\b"
PURPURE = #"\bpurpure\b"
VERT = #"\bvert\b"
TENNE
    = #"\btenne\b"
    | #"\btenné(?=( |$))"
ORANGE
    = #"\borange\b"
    | #"\bd'orange\b"
SANGUINE = #"\bsanguine\b"
MURREY = #"\bmurrey\b"
BLEU-CELESTE
    = #"\bbleu-+celeste\b"
    | #"\bbleu +celeste\b"
ROSE = #"\brose\b"
COPPER = #"\bcopper\b"
PROPER = #"\bproper\b"
WHITE = #"\bwhite\b"
BRUNATRE
    = #"\bbrunatre\b"
    | #"\bbrunâtre\b"
BUFF = #"\bbuff\b"

ERMINE = #"\bermine\b"
ERMINES = #"\bermines\b"
ERMINOIS = #"\berminois\b"
PEAN = #"\bpean\b"

BARRY = #"\bbarry\b"
BENDY = #"\bbendy\b"
BENDY-SINISTER
    = #"\bbendy sinister\b"
    | #"\bbendy\b" #"\bsinister\b"
CHEQUY = #"\bchequy\b"
CHEVRONNY = #"\bchevronny\b"
FRETTY = #"\bfretty\b"
GYRONNY = #"\bg[yi]ronny\b"
LOZENGY = #"\blozengy\b"
MASONY
    = #"\bmasony\b"
    | #"\bmasoned\b"
PALY = #"\bpaly\b"
PAPELLONY = #"\bpapellony\b"
PER-BEND
    = #"\bper +bend\b"
    | #"\bparty +per +bend\b"
PER-BEND-SINISTER
    = #"\bper +bend +sinister\b"
    | #"\bper +bend\b" #"\bsinister\b"
    | #"\bparty +per +bend +sinister\b"
    | #"\bparty +per +bend\b" #"\bsinister\b"
PER-CHEVRON
    = #"\bper +chevron\b"
    | #"\bparty +per +chevron\b"
PER-FESS
    = #"\bper +fess\b"
    | #"\bparty +per +fess\b"
PER-PALE
    = #"\bper +pale\b"
    | #"\bparty +per +pale\b"
PER-PILE
    = #"\bper +pile\b"
    | #"\bparty +per +pile\b"
PER-SALTIRE
    = #"\bper +saltire\b"
    | #"\bparty +per +saltire\b"
POTENTY = #"\bpotenty\b"
QUARTERED
    = #"\bquartered\b"
    | #"\bquarterly\b"
TIERCED-PER-FESS = #"\btierced +per +fess\b"
TIERCED-PER-PALE = #"\btierced +per +pale\b"
TIERCED-PER-PALL = #"\btierced +per +pall\b"
VAIRY = #"\bvairy\b"

PALE
    = #"\bpales?\b"
    | #"\bpallets?\b"
FESS
    = #"\bfess(es?)?\b"
    | #"\bbars?\b"
    | #"\bbarrulets?\b"
CHIEF = #"\bchief\b"
BASE = #"\bbase\b"
BEND
    = #"\bbends?\b"
    | #"\bbendlets?\b"
BEND-SINISTER
    = #"\bbends? +sinister\b"
    | #"\bbends?\b" #"\bsinister\b"
    | #"\bbendlets? +sinister\b"
    | #"\bbendlets?\b" #"\bsinister\b"
CROSS = #"\bcross(es)?\b"
SALTIRE = #"\bsaltires?\b"
CHEVRON
    = #"\bchevrons?\b"
    | #"\bchevronnels?\b"
PALL = #"\bpalls?\b"
PILE = #"\bpiles?\b"
GORE = #"\bgores?\b"
QUARTER = #"\bquarters?\b"
POINT
    = #"\bpoints?\b"
    | #"\bpoints? +sinister\b"
BORDURE = #"\bbordures?\b"
ORLE
    = #"\borles?\b"
    | #"\btressures?\b"
LABEL = #"\blabels?\b"

STRAIGHT = #"\bstraight\b"
INVECTED = #"\binvected\b"
ENGRAILED = #"\bengrailed\b"
EMBATTLED = #"\bembattled\b"
EMBATTLED-GRADY
    = #"\bembattled grady\b"
    | #"\bembattled-+grady\b"
    | #"\bembattled\b" #"\bgrady\b"
EMBATTLED-IN-CROSSES
    = #"\bembattled +in +crosses\b"
    | #"\bembattled-+in-+crosses\b"
    | #"\bembattled\b" #"\bin crosses\b"
POTENTY = #"\bpotenty\b"
INDENTED = #"\bindented\b"
DANCETTY = #"\bdancetty\b"
WAVY
    = #"\bwavy\b"
    | #"\bundy\b"
NEBULY = #"\bnebuly\b"
DOVETAILED
    = #"\bdovetailed\b"
    | #"\bdove +tailed\b"
    | #"\bdove-+tailed\b"
RAGULY = #"\braguly\b"
THORNY = #"\bthorny\b"
URDY = #"\burdy\b"
FIR-TREE-TOPPED
    = #"\bfir +tree +topped\b"
    | #"\bfir-+tree-+topped\b"
FIR-TWIGGED
    = #"\bfir +twigged\b"
    | #"\bfir-+twigged\b"
WOLF-TOOTHED
    = #"\bwolf +toothed\b"
    | #"\bwolf-+toothed\b"
RAYONNY = #"\brayonny\b"
ANGLED = #"\bangled\b"
BEVILLED = #"\bbevilled\b"
ENARCHED = #"\benarched\b"

HUMETTY
    = #"\bhumetty\b"
    | #"\bcouped\b"
VOIDED = #"\bvoided\b"
ENHANCED = #"\benhanced\b"
DEHANCED
    = #"\bdehanced\b"
    | #"\bdebased\b"
    | #"\babased\b"
    | #"\babaisse\b"
REVERSED
    = #"\breversed\b"
    | #"\binverted\b"
THROUGHOUT = #"\bthroughout\b"

CADENT = #"\bcadent\b"
COUCHANT = #"\bcouchant\b"
COURANT = #"\bcourant\b"
DORMANT = #"\bdormant\b"
PASCUANT = #"\bpascuant\b"
PASSANT = #"\bpassant\b"
RAMPANT = #"\brampant\b"
SALIENT = #"\bsalient\b"
SEGREANT = #"\bsegreant\b"
SEJANT = #"\bsejant\b"
SEJANT-ERECT
    = #"\bsejant[ -]erect\b"
    | #"\bsejant\b" "\berect\b"
STATANT = #"\bstatant\b"
ADDORSED = #"\baddorsed\b"
COMBATANT = #"\bcombatant\b"
RESPECTANT = #"\brespectant\b"
CLOSE = #"\bclose\b"
DISPLAYED = #"\bdisplayed\b"
IN-HER-PIETY
    = #"\bin her piety\b"
    | #"\bin-her-piety\b"
ISSUANT = #"\bissuant\b"
OVERT = #"\bovert\b"
RECURSANT = #"\brecursant\b"
RISING = #"\brising\b"
VIGILANT = #"\bvigilant\b"
VOLANT = #"\bvolant\b"
VULNING = #"\bvulning\b"
HAURIANT = #"\bhauriant\b"
NAIANT = #"\bnaiant\b"
URINANT = #"\burinant\b"
GLISSANT = #"\bglissant\b"
NOWED = #"\bnowed\b"

TO-DEXTER = #"\bto[ -]dexter\b"
TO-SINISTER = #"\bto[ -]sinister\b"
AFFRONTE = #"\baffronte\b"
EN-ARRIERE = #"\ben[ -]arriere\b"
GUARDANT = #"\bguardant\b"
IN-TRIAN-ASPECT
    = #"\bin trian aspect\b"
    | #"\bin-trian-aspect\b"
REGUARDANT = #"\breguardant\b"
SALIENT = #"\bsalient\b"

MIRRORED = #"\bmirrored\b"
ERASED = #"\berased\b"

ROUNDEL = #"\broundels?\b"
ANNULET = #"\bannulets?\b"
BILLET = #"\bbillets?\b"
ESCUTCHEON
    = #"\binescutcheons?\b"
    | #"\bescutcheons?\b"
LOZENGE = #"\blozenges?\b"
FUSIL = #"\bfusils?\b"
MASCLE = #"\bmascles?\b"
RUSTRE = #"\brustres?\b"
STAR
    = #"\bstars?\b"
    | #"\bmullets?\b"
CRESCENT = #"\bcrescents?\b"

LION = #"\blions?\b"

(* rules *)

root-field
    = root-variation
    | root-variation (AND* components)?
    | root-variation CHARGED WITH component (AND* components)?
    | <"("> root-field <")">

field
    = variation
    | variation (AND* components)?
    | variation CHARGED WITH component (AND* components)?
    | <"("> field <")">

root-variation
    = root-plain
    | partition

variation
    = plain
    | partition

root-plain
    = tincture

plain
    = tincture
    | COUNTERCHANGED

tincture
    = OR
    | ARGENT
    | SABLE
    | AZURE
    | GULES
    | PURPURE
    | VERT
    | TENNE
    | ORANGE
    | SANGUINE
    | MURREY
    | BLEU-CELESTE
    | ROSE
    | COPPER
    | PROPER
    | WHITE
    | BRUNATRE
    | BUFF
    | ERMINE
    | ERMINES
    | ERMINOIS
    | PEAN

partition
    = partition-with-two-fields
    | partition-with-three-fields-two-lines
    | partition-with-three-fields-three-lines
    | partition-with-four-fields
    | partition-with-eight-fields
    | partition-with-many-fields
    | partition-with-horizontally-repeating-fields
    | partition-with-vertically-repeating-fields
    | partition-with-repeating-fields-in-two-dimensions

(* used for suggestion labeling *)
partition-type
    = partition-type-with-two-fields
    | partition-type-with-three-fields-two-lines
    | partition-type-with-three-fields-three-lines
    | partition-type-with-four-fields
    | partition-type-with-eight-fields
    | partition-type-with-many-fields
    | partition-type-with-horizontally-repeating-fields
    | partition-type-with-vertically-repeating-fields
    | partition-type-with-repeating-fields-in-two-dimensions

partition-type-with-two-fields
    = PER-PALE
    | PER-FESS
    | PER-BEND
    | PER-BEND-SINISTER
    | PER-CHEVRON
partition-with-two-fields
    = partition-type-with-two-fields line? root-field AND root-field

partition-type-with-three-fields-two-lines
    = PER-PILE
    | TIERCED-PER-PALE
    | TIERCED-PER-FESS
partition-with-three-fields-two-lines
    = partition-type-with-three-fields-two-lines (line (AND line)?)? root-field AND root-field AND root-field

partition-type-with-three-fields-three-lines
    = TIERCED-PER-PALL
partition-with-three-fields-three-lines
    = partition-type-with-three-fields-three-lines (line (AND line)? (AND line)?)? root-field AND root-field AND root-field

partition-type-with-four-fields
    = PER-SALTIRE
    | QUARTERED
partition-with-four-fields
    = partition-type-with-four-fields line? root-field AND root-field (AND root-field)? (AND root-field)?

partition-type-with-eight-fields
    = GYRONNY
partition-with-eight-fields
    = partition-type-with-eight-fields line? root-field AND root-field (AND root-field)? (AND root-field)? (AND root-field)? (AND root-field)? (AND root-field)? (AND root-field)?

partition-type-with-many-fields
    = QUARTERED
partition-with-many-fields
    = partition-type-with-many-fields root-field AND root-field (AND root-field)*

(* used for suggestion labeling *)
layout-words
    = OF
    | X
    | TIMES
    | TRACTS

horizontal-layout
    = amount

vertical-layout
    = vertical-layout-implicit
    | vertical-layout-explicit

vertical-layout-implicit
    = amount

vertical-layout-explicit
    = amount TRACTS

layout-times
    = X
    | TIMES
    | AND

layout
    = horizontal-layout
    | vertical-layout-explicit
    | horizontal-layout layout-times? vertical-layout
    | vertical-layout-explicit layout-times? horizontal-layout

partition-type-with-repeating-fields-in-two-dimensions
    = CHEQUY
    | LOZENGY
    | VAIRY
    | POTENTY
    | PAPELLONY
    | MASONY
    | FRETTY
partition-with-repeating-fields-in-two-dimensions
    = partition-type-with-repeating-fields-in-two-dimensions (OF? layout)? root-plain AND root-plain

partition-type-with-horizontally-repeating-fields
    = PALY
partition-with-horizontally-repeating-fields
    = partition-type-with-horizontally-repeating-fields (OF? horizontal-layout)? root-field AND root-field

partition-type-with-vertically-repeating-fields
    = BARRY
    | BENDY
    | BENDY-SINISTER
    | CHEVRONNY
partition-with-vertically-repeating-fields
    = partition-type-with-vertically-repeating-fields (OF? vertical-layout)? root-field AND root-field

components
    = component (AND component)*

component
    = ordinary-group
    | charge-group

number-word
    = DIGIT-WORD
    | MULTI-WORD
    | TEEN-WORD
    | MULTIPLE-OF-TEN-WORD
    | MULTIPLE-OF-TEN-WORD #"-?" DIGIT-WORD

amount
    = A
    | NUMBER
    | number-word

between-or-with
    = BETWEEN
    | WITH
cottise
    = (line (AND line)?)? field
cottising
    = between-or-with? COTTISED WITH? cottise
    | between-or-with? COTTISED WITH? cottise AND cottise
    | between-or-with? DOUBLY COTTISED WITH? cottise
    | between-or-with? DOUBLY COTTISED WITH? cottise AND cottise

(* used for suggestion labeling *)
cottising-word
    = COTTISED
    | DOUBLY
    | BETWEEN
    | WITH

ordinary-group
    = amount? ordinary

(* used for suggestion labeling *)
ordinary-type
    = BASE
    | BEND
    | BEND-SINISTER
    | BORDURE
    | CHEVRON
    | CHIEF
    | CROSS
    | FESS
    | GORE
    | LABEL
    | ORLE
    | PALE
    | PALL
    | PILE
    | POINT
    | QUARTER
    | SALTIRE

(* used for suggestion labeling *)
ordinary-option
    = humetty-voided
    | enhanced-dehanced
    | reversed-throughout

<humetty-voided>
    = HUMETTY
    | VOIDED

<enhanced-dehanced>
    = ENHANCED
    | DEHANCED

<reversed-throughout>
    = REVERSED
    | THROUGHOUT

<humetty-voided-enhanced-dehanced>
    = humetty-voided
    | enhanced-dehanced

<humetty-voided-enhanced-dehanced-reversed>
    = humetty-voided
    | REVERSED

<humetty-voided-reversed-throughout>
    = humetty-voided
    | reversed-throughout

ordinary
    = LABEL field
    | BORDURE line? field
    | GORE line? field
    | (CHIEF
       | BASE
       | POINT) (humetty-voided-enhanced-dehanced* line?
                 | line? humetty-voided-enhanced-dehanced*) field cottising?
    | ORLE (line (AND line)?)? field
    | (CROSS
       | SALTIRE
       | QUARTER) (humetty-voided-enhanced-dehanced* (line (AND line)?)?
                   | (line (AND line)?)? humetty-voided-enhanced-dehanced*)
    | PILE (humetty-voided-reversed-throughout* (line (AND line)?)?
            | (line (AND line)?)? humetty-voided-reversed-throughout*) field cottising?
    | PALE (humetty-voided* (line (AND line)?)?
            | (line (AND line)?)? humetty-voided*) field (cottising (AND? cottising)?)?
    | (FESS
       | BEND
       | BEND-SINISTER) (humetty-voided-enhanced-dehanced* (line (AND line)?)?
                   | (line (AND line)?)? humetty-voided-enhanced-dehanced*) field (cottising (AND? cottising)?)?
    | (PALL
       | CHEVRON) (humetty-voided-enhanced-dehanced-reversed* (line (AND line (AND line)?)?)?
                   | (line (AND line (AND line)?)?)? humetty-voided-enhanced-dehanced-reversed*) field (cottising (AND? cottising (AND? cottising)?)?)?

line
    = line-type fimbriation?
    | fimbriation

line-type
    = STRAIGHT
    | INVECTED
    | ENGRAILED
    | EMBATTLED
    | EMBATTLED-GRADY
    | EMBATTLED-IN-CROSSES
    | POTENTY
    | INDENTED
    | DANCETTY
    | WAVY
    | NEBULY
    | DOVETAILED
    | RAGULY
    | THORNY
    | URDY
    | FIR-TREE-TOPPED
    | FIR-TWIGGED
    | WOLF-TOOTHED
    | RAYONNY
    | ANGLED
    | BEVILLED
    | ENARCHED

fimbriation
    = single-fimbriation
    | double-fimbriation

single-fimbriation
    = FIMBRIATED (WITH | OF)? tincture

double-fimbriation
    = FIMBRIATED (WITH | OF)? tincture AND? tincture

charge-group
    = amount? charge

(* used for suggestion labeling *)
charge-type
    = standard-charge-type
    | other-charge-type

charge
    = standard-charge-with-field
    | other-charge-with-field

standard-charge-with-field
    = standard-charge-type fimbriation? charge-options? field

standard-charge-type
    = ROUNDEL
    | ANNULET
    | BILLET
    | ESCUTCHEON
    | LOZENGE
    | FUSIL
    | MASCLE
    | RUSTRE
    | STAR
    | CRESCENT

other-charge-with-field
    = other-charge-type attitude? facing? charge-options? fimbriation? field

other-charge-type
    = LION

attitude
    = CADENT
    | COUCHANT
    | COURANT
    | DORMANT
    | PASCUANT
    | PASSANT
    | RAMPANT
    | SALIENT
    | SEGREANT
    | SEJANT
    | SEJANT-ERECT
    | STATANT
    | ADDORSED
    | COMBATANT
    | RESPECTANT
    | CLOSE
    | DISPLAYED
    | IN-HER-PIETY
    | ISSUANT
    | OVERT
    | RECURSANT
    | RISING
    | VIGILANT
    | VOLANT
    | VULNING
    | HAURIANT
    | NAIANT
    | URINANT
    | GLISSANT
    | NOWED

facing
    = TO-DEXTER
    | TO-SINISTER
    | AFFRONTE
    | EN-ARRIERE
    | GUARDANT
    | IN-TRIAN-ASPECT
    | REGUARDANT
    | SALIENT

charge-options
    = charge-option+

charge-option
    = MIRRORED
    | REVERSED
    | VOIDED
    | ERASED
